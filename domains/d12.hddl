(define (domain dom_noLang)
        (:requirements :negative-preconditions :typing :hierarchy)
        (:types
                victim - object
                location - object
                start - location
                dest - location
                sickbay - location
                player - object
                rubble - object
        )

        (:predicates
                (rescued ?arg0 - victim)
                (can_be_carried ?arg0 - victim)
                (healed ?arg0 - victim)
                (marked ?arg0 - victim)
                (blocked ?arg0 - dest)
                (at ?arg0 - victim ?arg1 - dest)
                (positioned ?arg0 - player ?arg1 - start)
                (free ?arg0 - player)
                (present ?arg0 - rubble ?arg1 - dest)
                (cleaned ?arg0 - room)
        )

        (:task t_clearing_room
                :parameters (?p_2 - player
                             ?rocks - rubble
                             ?room - dest)
        )

        (:task t_collaborate_carry_stabilized
                :parameters (?p_1 - player ?p_2 - player 
                             ?v - victim 
                             ?room - dest ?end - sickbay)
        )

        (:task t_collaborate_critical_wake
                :parameters (?p_1 - player ?p_2 - player
                             ?v - victim
                             ?pos - start ?room - dest)
        )

        (:task t_call_teammate
                :parameters (?p_1 - player ?p_2 - player
                             ?pos - start ?room - dest)
        )

        (:task megaTask
                :parameters (?p_1 - player ?p_2 - player
                             ?v - victim
                             ?rocks - rubble
                             ?pos - start ?room - dest ?end - sickbay)
        )

        (:method megaMethod
                :parameters (?p_1 - player ?p_2 - player
                             ?v - victim
                             ?rocks - rubble
                             ?pos - start ?room - dest ?end - sickbay)
                :task (megaTask ?p_1 ?p_2 ?v ?rocks ?pos ?room ?end)
                :precondition (free ?p_2)
                :ordered-subtasks (and
                   (t_call_teammate ?p_1 ?p_2 ?pos ?room)
                   (t_clearing_room ?p_2 ?rocks ?room)
                   (t_collaborate_critical_wake ?p_1 ?p_2 ?v ?pos ?room)
		   (t_collaborate_carry_stabilized ?p_1 ?p_2 ?v ?room ?end))
        )

        (:method m_clear_one_room
                :parameters (?p_2 - player
                            ?rocks - rubble
                            ?room - dest)
                :task (t_clearing_room ?p_2 ?rocks ?room)
                :precondition (and (blocked ?room)
                                   (present ?rocks ?room))
                :ordered-subtasks (and
                        (a_roomChange_approach_rubble ?p_2 ?rocks ?room)
                        (a_clear_rubble ?p_2 ?rocks ?room))
        )

        (:method m_come_wake_critical_768
                :parameters (?p_1 - player ?p_2 - player
                              ?v - victim
                             ?pos - start ?room - dest)
                :task (t_collaborate_critical_wake ?p_1 ?p_2 ?v ?pos ?room)
                :precondition (not (healed ?v))
                :ordered-subtasks (and 
                        (a_roomChange_locate_critical ?p_1 ?v ?room)
                        (a_gather_teammate ?p_1 ?p_2 ?pos ?room)
                        (a_triage ?p_1 ?p_2 ?v ?room))
        )

        (:method m_carry_unmarked_stabilized_1234
                :parameters (?p_1 - player ?p_2 - player
                             ?v - victim 
                             ?room - dest ?end - sickbay)
                :task (t_collaborate_carry_stabilized ?p_1 ?p_2 ?v ?room ?end)
                :precondition (and (healed ?v)
                                   (not (marked ?v)))
                :ordered-subtasks (and 
                         (a_roomChange_find_victim ?p_1 ?v ?room)
                         (a_mark_victim ?p_1 ?v ?room)
                         (a_select_vic_to_carry ?p_1 ?v ?room)
                         (a_evacuate_victim ?p_2 ?v ?room ?end)) 
        )

        (:method m_carry_stabilized4
                :parameters (?p_1 - player 
                             ?p_2 - player
                             ?v - victim 
                             ?room - dest ?end - sickbay)
                :task (t_collaborate_carry_stabilized ?p_1 ?p_2 ?v ?room ?end)
                :precondition (and (marked ?v) 
                                   (healed ?v))
                :ordered-subtasks (and 
                         (a_select_vic_to_carry ?p_1 ?v ?room)
                         (a_evacuate_victim ?p_2 ?v ?room ?end)) 
        )

        (:method m_collaborate_team_56
                :parameters (?p_1 - player ?p_2 - player
                            ?pos - start ?room - dest)
                :task (t_call_teammate ?p_1 ?p_2 ?pos ?room)
                :precondition (free ?p_2)
                :ordered-subtasks (and 
                         (a_locate_teammate ?p_1 ?p_2 ?pos ?room)
                         (a_gather_teammate ?p_1 ?p_2 ?pos ?room))
        )

        (:action a_roomChange_find_victim
         ; Events reflected: room_change
         ;any regular or critical victim
                :parameters (?p_1 - player
                             ?v - victim
                             ?room - dest)
                :precondition (not (marked ?v))
                :effect (and (at ?v ?room)
                             (positioned ?p_1 ?room))
        )

        (:action a_mark_victim
         ; Events reflected: aggregated 'Marker'
                :parameters (?p_1 - player
                             ?v - victim
                             ?room - dest)
                :precondition (and (at ?v ?room)
;new                                   (not (marked ?v))
                                   (positioned ?p_1 ?room))
                :effect (marked ?v)
        )

        (:action a_select_vic_to_carry
         ; Events reflected: ItemPickedUp, ItemUsed
         ; to reflect why some victims are chosen over others and that victims
            ; to be carried must be marked first
                :parameters (?p_1 - player 
                             ?v - victim 
                             ?room - dest)
                :precondition (and (marked ?v)
                                   (healed ?v))
                :effect (and (can_be_carried ?v)
                             (at ?v ?room)
                             (not (free ?p_1))
                             (positioned ?p_1 ?room))
        )

        (:action a_evacuate_victim
         ; Events reflected:room_change, toolused, marker, items
                           ; victim evacuated to proper sickbay
         ; reflects room change.
                :parameters (?p_2 - player 
                             ?v - victim 
                             ?room - dest ?end - sickbay)
                :precondition (and (healed ?v)
                                   (marked ?v)
                                   (can_be_carried ?v)
                                   (at ?v ?room)
                                   (positioned ?p_2 ?room))
                :effect (and (at ?v ?end) 
                             (rescued ?v)
                             (not (at ?v ?room))
                             (positioned ?p_2 ?end)
                             (free ?p_2)
                             (not (positioned ?p_2 ?room)))
        )

        (:action a_locate_teammate
         ; Events Reflected: room change, dialog event
                :parameters (?p_1 - player ?p_2 - player
                             ?pos - start ?room - dest)
                :precondition (free ?p_2)
                :effect (positioned ?p_1 ?room)
        ; the logic is that if asked, they are in different areas
        ; the asker is then assumed to be in destination of interest
        ; the answerer remains free since they were not beckoned
        )

        (:action a_gather_teammate
        ; Events Reflected: room change, dialog event, ProxBlockInt
        ; player 2 is summoned, no longer free and no longer at the 
        ;   location they were in before being summoned. This action 
        ;   does not reflect language or ability to reject/accept.
                :parameters (?p_1 - player ?p_2 - player
                             ?pos - start ?room - dest)
                :precondition (positioned ?p_1 ?room)
                :effect (and (positioned ?p_2 ?room)
                             (not (free ?p_2))
                             (not (positioned ?p_2 ?pos)))
        )

        (:action a_roomChange_locate_critical
         ;Events Reflected: room change
                :parameters (?p_1 - player 
                             ?v - victim
                             ?room - dest)
                :precondition (and (not (healed ?v))
                                   (at ?v ?room))
                :effect (positioned ?p_1 ?room)
        )

        (:action a_triage
         ; Events Reflected: Tool used, ProxBlockInt, Triage
                :parameters (?p_1 - player ?p_2 - player
                             ?v - victim
                             ?room - dest)
                :precondition (and (not (healed ?v))
                                   (at ?v ?room)
                                   (positioned ?p_1 ?room)
                                   (positioned ?p_2 ?room))
                :effect (healed ?v)
        )

        (:action a_roomChange_approach_rubble
         ; Events Reflected: room change
                :parameters (?p_2 - player
                             ?rocks - rubble
                             ?room - dest)
                :precondition (and (blocked ?room)
                                   (present ?rocks ?room))
                :effect (positioned ?p_2 ?room)
        )

        (:action a_clear_rubble
         ;Events Reflected: tool used, rubble destroyed
                :parameters (?p_2 - player
                             ?rocks - rubble
                             ?room - dest)
                :precondition (and (positioned ?p_2 ?room)
                                   (blocked ?room)
                                   (present ?rocks ?room))
                :effect (not (present ?rocks ?room))
        )
);end define domain
