(define (domain dom_noLang)
        (:requirements :negative-preconditions :typing :hierarchy)
        (:types
                human - object
                victim - object
                location - object
                sickbay - object
                player - object
                rubble - object
        )

        (:predicates
                (rescued ?arg0 - victim)
                (can_be_carried ?arg0 - victim)
                (retrieved ?arg0 - victim)
                (healed ?arg0 - victim)
                (awakened ?arg0 - victim)
                (typed ?arg0 - victim)
                (blocked ?arg0 - location)
                (at ?arg0 - human ?arg1 - location)
                (free ?arg0 - player)
                (present ?arg0 - rubble ?arg1 - location)
                (cleaned ?arg0 - room)
                (matched ?arg0 - victim ?arg1 - location)
                (proximity ?arg0 - player ?arg1 - location)
                (is_medic ?arg0 - player)
                (is_type_A ?arg0 - victim)
                (is_type_B ?arg0 - victim)
                (is_type_C ?arg0 - victim)
        )


; ----------------------------------------------------------------------

        (:task go_type_the_victim
                :parameters (?p_1 - player
                             ?v - victim
                             ?pos - location ?room - location)
        )

        (:task go_break_rubble
                :parameters (?p_1 - player
                             ?rocks - rubble
                             ?pos - location ?room - location)
        )
     
        (:task gather_teammate
                :parameters (?p_1 - player ?p_2 - player ?p_3 - player
                             ?pos1 ?pos2 ?pos3 - location ?room - location)
        )

        (:task triage_victim
                :parameters (?p_1 - player ?v - victim
                             ?room - location)
        )

        (:task go_triage_victim
                :parameters (?p_1 - player ?v - victim
                             ?pos ?room - location)
        )

        (:task go_pickup_victim
                :parameters (?p_1 - player ?v - victim
                             ?pos ?room - location)
        )

        (:task transport_victim
                :parameters (?p_1 - player ?v - victim
                             ?pos ?room - location)
        )

        (:task go_transport_victim
                :parameters (?p_1 - player ?v - victim
                             ?pos ?room ?final - location)
        )   

        (:task gather_to_wake
                :parameters (?p_1 ?p_2 - player 
                             ?v - victim 
                             ?pos ?room ?final - location)
        )

        (:task wake_triage_critical
                :parameters (?p_1 ?p_2 - player 
                             ?v - victim 
                             ?final - location)
        )

        (:task evacuate_victim
                :parameters (?p_1 - player ?v - victim
                             ?room - location ?sb - sickbay)
        )

        (:method m_evacuate_victim_A
          ;not the same as go_transport_victim.
                :parameters (?p_1 - player ?v - victim
                             ?room - location ?sb - sickbay)
                :task (evacuate_victim ?p_1 ?v ?room ?sb)
                :precondition (and (at ?p_1 ?room) 
                                   (healed ?v)
                                   (is_type_A ?v)
                                   (typed ?v)
                                   (at ?v ?room))
                :ordered-subtasks (and
                    (pickup_victim ?p_1 ?v ?room)
                    (carry_sickbay ?p_1 ?v ?room ?sb)
                    (victim_evacuated ?p_1 ?v ?sb))
        )


        (:method m_wake_triage_critical
                :parameters (?p_1 ?p_2 - player ?v - victim ?final - location)
                :task (wake_triage_critical ?p_1 ?p_2 ?v ?final)
                :precondition (and (at ?v ?final)
                                   (at ?p_1 ?final)
                                   (at ?p_2 ?final)
                                   (not (healed ?v)))
                :ordered-subtasks (and
                    (wake_critical ?p_1 ?p_2 ?v ?final)
                    (triage_victim ?p_1 ?v ?final))
        )

        (:method m_gather_to_wake
                :parameters (?p_1 ?p_2 - player 
                             ?v - victim 
                             ?pos ?room ?final - location)
                :task (gather_to_wake ?p_1 ?p_2 ?v ?pos ?room ?final)
                :precondition (and (at ?p_1 ?pos)
                                   (is_medic ?p_1)
                                   (at ?p_2 ?room)
                                   (at ?v ?final)
                                   (not (awakened ?v)))
                :ordered-subtasks (and
                    (location ?p_1 ?pos ?final)
                    (location ?p_2 ?room ?final))
        )

        (:method m_go_transport_victim
                :parameters ( ?p_1 - player ?v - victim
                              ?pos ?room ?final - location)
                :task (go_transport_victim ?p_1 ?v ?pos ?room ?final)
                :precondition (and (at ?p_1 ?pos)
                                  (at ?v ?room))
                :ordered-subtasks (and
                    (location ?p_1 ?pos ?room)
                    (pickup_victim ?p_1 ?v ?room)
                    (carry_location ?p_1 ?v ?room ?final)
                    (place_victim ?p_1 ?v ?final))
        ) 

        (:method m_transport_victim
          ;not the same as evacution or rescue.
          ;not the same as come_transport_victim either.
                :parameters (?p_1 - player ?v - victim
                             ?pos ?room - location)
                :task (transport_victim ?p_1 ?v ?pos ?room)
                :precondition (and (at ?p_1 ?pos) 
                                   (at ?v ?pos)
                                   (retrieved ?v))
                :ordered-subtasks (and
                    (carry_location ?p_1 ?v ?pos ?room)
                    (place_victim ?p_1 ?v ?room))
        )

        (:method m_go_pickup_victim
                :parameters (?p_1 - player ?v - victim
                             ?pos ?room - location)
                :task (go_pickup_victim ?p_1 ?v ?pos ?room)
                :precondition (and (at ?v ?room) (at ?p_1 ?pos)
                                   (not (at ?p_1 ?room)))
                :ordered-subtasks (and
                    (location ?p_1 ?pos ?room)
                    (pickup_victim ?p_1 ?v ?room))
        )

        (:method m_go_triage_victim
                :parameters (?p_1 - player ?v - victim
                             ?pos ?room - location)
                :task (go_triage_victim ?p_1 ?v ?pos ?room)
                :precondition (and (at ?v ?room)
                                   (at ?p_1 ?pos)
                                   (not (healed ?v)))
                :ordered-subtasks (and
                    (location ?p_1 ?pos ?room)
                    (triage_victim ?p_1 ?v ?room))
        )

        (:method m_go_type_the_victim
                :parameters (?p_1 - player
                             ?v - victim
                             ?pos - location ?room - location)
                :task (go_type_the_victim ?p_1 ?v ?pos ?room)
                :precondition (at ?p_1 ?pos)
                :ordered-subtasks (and
                   (location ?p_1 ?pos ?room)
                   (type_the_victim ?p_1 ?v ?room))
        )

        (:method m_go_break_rubble
                :parameters (?p_1 - player
                             ?rocks - rubble
                             ?pos - location ?room - location)
                :task (go_break_rubble ?p_1 ?rocks ?pos ?room)
                :precondition (and (at ?p_1 ?pos)
                                   (present ?rocks ?room))
                :ordered-subtasks (and
                   (location ?p_1 ?pos ?room)
                   (rubble_destroyed ?p_1 ?rocks ?room))
        )

        (:method m_gather_full_team
                :parameters (?p_1 ?p_2 ?p_3 - player
                             ?pos1 ?pos2 ?pos3 ?room - location)
                :task (gather_teammate ?p_1 ?p_2 ?p_3 ?pos1 ?pos2 ?pos3 ?room)
                :precondition ()
                :ordered-subtasks (and
                      (location ?p_1 ?pos1 ?room)
                      (location ?p_2 ?pos2 ?room)
                      (location ?p_3 ?pos3 ?room))
       )

;-----------------------------------------------------------------------
        (:action place_victim
         ;observations/events/player/victim_placed
                :parameters (?p_1 - player ?v - victim 
                             ?room - location)
                :precondition (and (at ?p_1 ?room)
                                   (at ?v ?room)
                                   (retrieved ?v))
                :effect (not (retrieved ?v))
        )

        (:action victim_evacuated
         ;observations/events/player/victim_evacuated
                :parameters (?p_1 - player ?v - victim 
                             ?sb - sickbay)
                :precondition (and (at ?p_1 ?sb)
                                   (at ?v ?sb)
                                   (healed ?v)
                                   (typed ?v)
                                   (retrieved ?v))
                :effect (and (rescued ?v)
                             (not (retrieved ?v))
                             (not (at ?v ?sb))
                             (not (at ?p_1 ?sb)))
        )

        (:action pickup_victim
         ;observations/events/player/victim_picked_up
                :parameters (?p_1 - player ?v - victim 
                             ?room - location)
                :precondition (and (at ?p_1 ?room)
                                   (at ?v ?room))
                :effect (retrieved ?v)
        )

        (:action triage_victim
         ;observations/events/player/triage
                :parameters (?p_1 - player 
                             ?v - victim
                             ?room - location)
                :precondition (and (not (healed ?v))
                                   (at ?v ?room)
                                  (at ?p_1 ?room))
                :effect (healed ?v)
        )

        (:action rubble_destroyed
         ;observations/events/player/rubble_destroyed
                :parameters (?p_1 - player
                             ?rocks - rubble
                             ?room - location)
                :precondition (and (at ?p_1 ?room)
                                   (present ?rocks ?room))
                :effect (not (present ?rocks ?room))
        )


        (:action carry_sickbay
         ;observations/events/player/location
         ;observations/events/player/victim_evacuated
             :parameters (?p_1 - player ?v - victim
                          ?room - location
                          ?sb - sickbay)
             :precondition (and (at ?p_1 ?room)
                                (at ?v ?room) 
                                (retrieved ?v))
             :effect (and (at ?p_1 ?sb) 
                          (at ?v ?sb)
                          (not (at ?p_1 ?room))
                          (not (at ?v ?room)))
        )

        (:action carry_location
         ;observations/events/player/location
         ;observations/events/player/victim_placed
         ;when a player changes room carrying a victim
             :parameters (?p_1 - player ?v - victim
                          ?pos ?room - location)
             :precondition (and (at ?p_1 ?pos)
                                (at ?v ?pos) 
                                (retrieved ?v))
             :effect (and (at ?p_1 ?room) 
                          (at ?v ?room)
                          (not (at ?p_1 ?pos))
                          (not (at ?v ?pos)))
        )

        (:action location
         ;observations/events/player/location
         ;when a player changes room
             :parameters (?p_1 - player
                          ?pos - location
                          ?room - location)
             :precondition (at ?p_1 ?pos)
             :effect (and (at ?p_1 ?room)
                          (not (at ?p_1 ?pos)))
        )

        (:action type_the_victim
         ; Events reflected: aggregated 'Marker'
                :parameters (?p_1 - player
                             ?v - victim
                             ?room - location)
                :precondition (and (at ?v ?room)
                                   (at ?p_1 ?room))
                :effect (typed ?v)
        )

        (:action gather_to_wake
        ; requires the medic and one other player to gather.
        ; assumes medic and other player are in different rooms than the
        ; victim. does not move players out of room. Wakes victim.
        ; does not heal victim.
                :parameters (?p_1 ?p_2 - player 
                             ?v - victim ?pos ?room ?final - location)
                :precondition (and (at ?p_1 ?pos)
                                   (is_medic ?p_1)
                                   (at ?p_2 ?room)
                                   (at ?v ?final)
                                   (not (awakened ?v)))
                :effect (and (at ?p_1 ?final)
                             (at ?p_2 ?final)
                             (proximity ?p_1 ?final)
                             (proximity ?p_2 ?final))
        )

        (:action wake_critical
                :parameters (?p_1 ?p_2 - player ?v - victim ?room - location)
                :precondition (and (is_medic ?p_1)
                                   (at ?p_1 ?room)
                                   (at ?p_2 ?room)
                                   (proximity ?p_1 ?room)
                                   (proximity ?p_2 ?room)
                                   (at ?v ?room))
                :effect (and (awakened ?v)
                             (not (proximity ?p_1 ?room))
                             (not (proximity ?p_2 ?room))
                             (not (at ?p_2 ?room)))
        )
); end domain definition
